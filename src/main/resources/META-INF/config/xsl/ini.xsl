<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:xs="http://www.w3.org/2001/XMLSchema" xmlns:c="http://baxter-it.com/config/component"
	xmlns:conf="http://baxter-it.com/config"
	xmlns:gp="http://baxter-it.com/currency-dealing/conf/generic-properties"
	exclude-result-prefixes="xs c conf gp" version="2.0">

	<xsl:import href="baxterxsl:repo-base.xsl" />
	<xsl:import href="baxterxsl:text-fmt.xsl" />
	<xsl:import href="baxterxsl:conf-reference.xsl" />

	<xsl:output method="text" encoding="UTF-8"
		omit-xml-declaration="yes" />

	<xsl:param name="configurationComponentId" />

	<xsl:template match="/">
		<xsl:variable name="root">
			<xsl:copy-of select="conf:configuration-source/conf:request" />
			<xsl:apply-templates select="conf:configuration-source/conf:request"
				mode="load-document-with-variants">
				<xsl:with-param name="prefix" select="'ini'" />
			</xsl:apply-templates>
		</xsl:variable>
		<xsl:apply-templates select="$root/gp:configuration" />
	</xsl:template>

	<xsl:template match="gp:configuration">
		<xsl:text># Hi, I am an INI file. Generated by CD Config Processor</xsl:text>
		<xsl:call-template name="CRLF" />
		<xsl:apply-templates
			select="gp:property[c:component[@id=$configurationComponentId]] | conf:reference[c:component[@id=$configurationComponentId]]" />
	</xsl:template>

	<xsl:template match="conf:reference">
		<xsl:value-of select="@id" />
		<xsl:text>=</xsl:text>
		<xsl:apply-templates select="." mode="url" />
		<xsl:call-template name="CRLF" />
	</xsl:template>

	<xsl:template match="gp:property">
		<xsl:value-of select="@id" />
		<xsl:text>=</xsl:text>
		<xsl:value-of select="@value" />
		<xsl:call-template name="CRLF" />
	</xsl:template>

</xsl:stylesheet>
